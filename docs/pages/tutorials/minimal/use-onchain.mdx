# Use MUD from onchain code

In this tutorial you will learn how to use a `World` and the systems in it from Solidity code (via forge scripts in your command line or via deployed contracts).
You can use this knowledge to add sub-systems to a `World` or to access MUD state in contracts outside the `World`.

## Setup

1. [Create a minimal application](../minimal) and run it.

1. In a separate command line window, clone the MUD repository and install the necessary packages to run the `onchain-client` example project.

   ```sh copy
   git clone https://github.com/latticexyz/mud.git
   cd mud/examples/onchain-client/
   pnpm install
   ```

1. Set the necessary environment variables in `.env`. This file specifies two variables:

   | Variable    | Meaning                                                                                                                 |
   | ----------- | ----------------------------------------------------------------------------------------------------------------------- |
   | PRIVATE_KEY | A private key that has ETH on the blockchain. This is a different private key than the one that deployed the `World`.   |
   | WORLD_ADDR  | The address of the `World` contract. The value in `.env` is the initial world deployed by `pnpm dev`, change as needed. |

   If these values need to be different from the default ones, edit `.env` appropriately.

1. Run the MUD codegen to generate table libraries and World interfaces from your MUD config.

   ```sh copy
   pnpm build:mud
   ```

<details>

<summary>Generating MUD code</summary>

The definitions for this `World` are contained in two files:

- [`mud.config.ts` which contains the data schema](/tutorials/walkthrough/minimal-onchain#mudconfigts).
- [`src/systems/IncremetSystem.sol`, which contains the logic](https://mud.dev/tutorials/walkthrough/minimal-onchain#incrementsystemsol).

When we run `pnpm build:mud`, MUD generates the files in `src/codegen` that represent this `World` in Solidity.
You can read a description of the resulting data definitions [in the onchain walkthrough](/tutorials/walkthrough/minimal-onchain) (expand **The autogenerated files**).
The `World` definition in `src/codegen/world/IWorld.sol` simply tells other Solidity files about the functions that are included in the `World`, including `increment`.

</details>

## Use the `World` from a forge script

Before we actually use a contract, we'll use a [forge script](https://book.getfoundry.sh/reference/forge/forge-script),
The advantage of scripts is that they allow you to run Solidity code from your command line without deploying a contract, making it easier to experiment.

To run the script, use this command:

```sh copy
pnpm script
```

You can check `package.json` to see that the script is executed by this command:

```sh
forge script --rpc-url http://localhost:8545 --broadcast script/ScriptClient.s.sol:ScriptClient
```

- `--rpc-url` specifies the URL to an RPC node of the blockchain we want to interact with.
  In this case, this is one that runs locally.
  If your blockchain runs anywhere else, modify as needed.
- `--broadcast` tells Foundry that we don't want to just simulate the transactions, but actually send them to the blockchain
- `script/ScriptClient.s.sol:ScriptClient` is the contract to run as a script.
  By convention, forge scripts are stored in `script` and have the extension `.s.sol`.

<details>

<summary>Content of `script/ScriptClient.s.sol`</summary>

```solidity
// SPDX-License-Identifier: MIT
pragma solidity >=0.8.0;
```

This is standard Solidity boilerplate.

```solidity
import { Script } from "forge-std/Script.sol";
import { console } from "forge-std/console.sol";
```

These lines are standard Foundry imports:

- `lib/forge-std/Script.sol` is the script definitions.
- `lib/forge-std/console.log` lets us emit console messages (you can see them in the output of the command above).

```solidity
import { Counter } from "codegen/Tables.sol";
import { IWorld } from "codegen/world/IWorld.sol";
```

These lines import from the generated MUD code the definitions we need:

- `Counter`, the counter with which we wish to interact.
- `IWorld`, an interface to the `World` with all the functions from the various systems.

```solidity
contract ScriptClient is Script {
  IWorld world;

  function run() external {
```

This is the function that runs the script.

```solidity
    // Load the private key from the `PRIVATE_KEY` environment variable (in .env)
    uint256 myPrivateKey = vm.envUint("PRIVATE_KEY");

    // Start broadcasting transactions from that account
    vm.startBroadcast(myPrivateKey);
```

Read the private key from the environment (which includes the content of the `.env` file) and use it to issue transactions.

```solidity
    // Connect to the world using `WORLD_ADDR` from .env
    console.log("Connecting to world at ", vm.envAddress("WORLD_ADDR"));
    world = IWorld(vm.envAddress("WORLD_ADDR"));
```

Connect to the `World` at `WORLD_ADDR`.

```solidity
    console.log("The counter values is now ", Counter.get(world));

    console.log("Incrementing the counter");
    world.increment();

    console.log("The counter values is now ", Counter.get(world));
```

This is the actual script.
Note that it has access to `Counter.get(world)` to read the data, but to modify it you have to go through a `System`.
Remember, _there are no secrets on the blockchain_ and therefore we can assume that anybody can read anything at any time after it is posted - so we might as well provide read access directly.

<details>

<summary>Try to bypass security</summary>

There is another script `BypassSecurity`, which attempts to bypass the `System` logic and set `Counter` directly.
You can run it with this command.

```sh copy
forge script --rpc-url http://localhost:8545 --broadcast script/ScriptClient.s.sol:BypassSecurity
```

It fails with this error, because we don't have the necessary access rights to write to the `Counter` table:

```
== Logs ==
  Connecting to world at  0x5FbDB2315678afecb367f032d93F642f64180aa3
  Just before trying to reset the counter
Error:
e64c0348:ROOT_NAMESPACE/Counter
```

This is the relevant portion of the code.

```solidity
    // Try to set `Counter` directly. See that it fails.
    console.log("Just before trying to reset the counter");
    Counter.set(world, 0);
    console.log("Just after trying to reset the counter");
```

We know the the failure happens in the `Counter.set(world, 0);` line because we see the log entry for the line before it, but not the one for the line immediately after it.

</details>

```solidity
    // Stop broadcasting transactions from the account
    vm.stopBroadcast();
  }
}
```

Finally, stop broadcasting transactions using the private key.

</details>

## Use the `World` from a contract

You can use the contract [in the examples directory](https://github.com/latticexyz/mud/tree/main/examples/onchain-client).

1. Read the environment variables.

   ```sh copy
   . .env
   ```

1. Deploy the sample contact.

   ```sh copy
   forge create OnchainClient --private-key $PRIVATE_KEY --constructor-args $WORLD_ADDR
   ```

1. Set `CLIENT_ADDR` to the address to which the contract is deployed.

1. Get the value of `Counter` from the client.

   ```sh copy
   cast call $CLIENT_ADDR "readCounter()"
   ```

1. Increment `Counter` and read the value again.
   See it is one more.

   ```sh copy
   cast send --private-key $PRIVATE_KEY $CLIENT_ADDR "add1()"
   cast call $CLIENT_ADDR "readCounter()"
   ```

1. Attempt to reset the counter, which bypasses the `System` logic and modifies `Counter` directly.
   See that this attempt fails, since our contract doesn't have the necessary access rights to write to the `Counter` table.

   ```sh copy
   cast send --private-key $PRIVATE_KEY $CLIENT_ADDR "reset()"
   ```

   The error message should be similar to:

   ```
   (code: 3, message: execution reverted, data: Some(String("0x")))
   ```

<details>

<summary>Explanation of `src/OnchainClient.sol`</summary>

Let's go over the contract source code (`src/OnchainClient.sol`).

```solidity
// SPDX-License-Identifier: MIT
pragma solidity >=0.8.0;

import { Counter } from "codegen/Tables.sol";
import { IWorld } from "codegen/world/IWorld.sol";
```

We import `Counter` and `IWorld` from the code that was generated using `mud codegen`.

```solidity
contract OnchainClient {
  IWorld immutable world;

  constructor(address world_) {
    world = IWorld(world_);
  }
```

Define a variable to hold the address of the `World`.
The constructor receives the value for it.

```solidity
function readCounter() public view returns (uint32) {
  return Counter.get(world);
}
```

To get the value of the counter we use the `Counter` library we imported from `codegen/Tables.sol`.
There are two versions of `get` in that library.

[This version](https://github.com/latticexyz/mud/blob/230822-use-onchain/examples/onchain-client/lib/codegen/tables/Counter.sol#L60-L66) reads from the `World` of which it is a part.
It is the version you use in a `System` deployed to that `World`.

[This version](https://github.com/latticexyz/mud/blob/230822-use-onchain/examples/onchain-client/lib/codegen/tables/Counter.sol#L68-L74) gets a parameter which is the address of the `World`.
It is the version you use in code which is not part of the `World` with the relevant table.

```
  function add1() public {
    world.increment();
  }
```

When a `System` is in the root namespace of a `World`, its functions are available from the `World` itself, so we can call the function on the `World` to execute the `System` logic.

```
  function reset() public {
    Counter.set(world, 0);
  }
}
```

This function attempts to bypass the `System` logic and set the counter value directly.
As you have already seen, it fails because the contract doesn't have the necessary access rights to write to the `Counter` table.

</details>

## Setting up a Foundry project for MUD access

The example is already set up for MUD access, but these are the steps you do to add it to other Foundry projects.

1. Create the Foundry project.

   ```sh copy
   forge init
   ```

1. At the root of the Foundry project, create a Node project for the libraries.
   Solidity libraries are usually published as [npm packages](https://www.npmjs.com/) and ours are no exception.

   ```sh copy
   pnpm init
   pnpm install @latticexyz/world@next @latticexyz/store@next @latticexyz/schema-type@next @latticexyz/cli@next
   ```

1. Create a remapping file to inform Foundry where the MUD libraries are located.

   ```text filename="remappings.txt" copy
   @latticexyz=node_modules/@latticexyz
   ```

1. Copy `mud.config.ts` to the root directory of the project and the `System` source files into `src/systems` (which you'll need to create).

1. Run code generation.

   ```sh copy
   pnpm mud tablegen
   pnpm mud worldgen
   ```
